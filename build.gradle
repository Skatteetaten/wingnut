import java.text.SimpleDateFormat
import java.util.Date
import no.skatteetaten.aurora.gradle.plugins.docker.DockerTools
import no.skatteetaten.aurora.gradle.plugins.docker.ProcessTools
import java.io.*

buildscript {
  repositories {
    maven {
      url "${nexusUrl}/content/groups/public"
    }
  }
  dependencies {
    classpath(
        "no.skatteetaten.aurora.gradle.plugins:aurora-gradle-plugin:1.1.0",
        "no.skatteetaten.aurora.gradle.plugins:aurora-docker-plugin:1.0.1"
    )
  }
}

//TODO move to plugin
def static buildTime() {
  def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  df.setTimeZone(TimeZone.getTimeZone("UTC"))
  return df.format(new Date())
}

def static gitSha() {
  def cmd = "git rev-parse --short HEAD"
  def proc = cmd.execute()
  return proc.text.trim()

}

ext.aurora = [
    requireStaging: false
]

apply plugin: 'base'
apply plugin: 'no.skatteetaten.plugins.aurora'
apply plugin: 'no.skatteetaten.plugins.aurora-docker'

auroradocker {
  imageName = 'aurora/wingnut'
  workingDir = "src/main/docker"
  buildArgs = [
      jolokiaVersion: "1.3.7",
      javaMajorVersion: "8",
      javaMinorVersion: "131",
      javaBuildVersion: "11-r2",
      baseImageVersion: version,
      buildDate: buildTime(),
      gitSha: gitSha()
  ]
}


task tagTestImage() {
  dependsOn buildImage
  doLast {
    println "Tag test build image for testing"

    String cmd = """docker tag aurora/wingnut:$version aurora/wingnut:testing"""


    println cmd
    ProcessTools.Result result = ProcessTools.runCommand(cmd)
    if (result.process.exitValue() != 0) {
      throw new GradleException("Image build failed. Inspect output.")
    }
    ext.imageId = DockerTools.getImageIdFromOutput(result.output)
  }
}

task testImage() {
  dependsOn tagTestImage
  doLast {
    println "Build example image"

    String cmd = """docker build example"""

    println cmd
    ProcessTools.Result result = ProcessTools.runCommand(cmd)
    if (result.process.exitValue() != 0) {
      throw new GradleException("Image build failed. Inspect output.")
    }
    ext.imageId = DockerTools.getImageIdFromOutput(result.output)
  }
}


task testOutputImage() {
  dependsOn testImage
  doLast {
    ProcessTools.Result result = ProcessTools.runCommand("""docker run -m 512m --memory-swap -1 --cpu-period=100000 --cpu-quota=150000 -i $testImage.imageId""")
    if (result.process.exitValue() != 0) {
      throw new GradleException("Unable to run image. Inspect output for details.")
    }
    assert result.output.contains("JAVA_OPTS=-Xmx410m -XX:ParallelGCThreads=2 -XX:ConcGCThreads=2 -Djava.util.concurrent.ForkJoinPool.common.parallelism=2")
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = "3.4"
}
